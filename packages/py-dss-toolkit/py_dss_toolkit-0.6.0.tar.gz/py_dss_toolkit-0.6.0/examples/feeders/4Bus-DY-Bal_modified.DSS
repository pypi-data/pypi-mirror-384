clear
Set DefaultBaseFrequency=60

! IEEE 4-bus test case   D-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4busDYBal basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 normamps=530
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 normamps=340

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 bus1=sourcebus bus2=n2 geometry=4wire length=200 units=ft 

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Delta-Ygrd
new transformer.t1 xhl=6
~ wdg=1 bus=n2 conn=delta kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye   kV=4.16  kVA=6000 %r=0.5

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=250 units=ft  

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=n4 conn=wye kV=4.16 kW=5400 pf=0.9  model=1

! **** Inserted by Guilherme
new transformer.t2a phases=1 windings=2 xhl=6 %loadloss=0.5 %noloadloss=0.1 %imag=0.5
~ wdg=1 bus=n4.1   kv=2.40178 kva=300 conn=wye
~ wdg=2 bus=n5.1.3 kv=0.22    kva=300 conn=delta

new transformer.t2b phases=1 windings=2 xhl=6 %loadloss=0.5 %noloadloss=0.1 %imag=0.5
~ wdg=1 bus=n4.2   kv=2.40178 kva=300 conn=wye
~ wdg=2 bus=n5.2.1 kv=0.22    kva=300 conn=delta

new transformer.t2c phases=1 windings=2 xhl=6 %loadloss=0.5 %noloadloss=0.1 %imag=0.5
~ wdg=1 bus=n4.3   kv=2.40178 kva=300 conn=wye
~ wdg=2 bus=n5.3.2 kv=0.22    kva=300 conn=delta 

new line.line3 bus1=n5 bus2=n6 geometry=4wire length=100 units=ft

new line.line4 bus1=n6 bus2=n7 geometry=4wire length=100 units=ft  

new load.load2 phases=3 bus1=n7 conn=delta kV=0.22 kW=200 pf=1  model=1
! ****

set voltagebases=[12.47, 4.16, 0.22, 0.127] 
calcvoltagebases     ! **** let DSS compute voltage bases

set mode=SnapShot
set controlmode=Static

solve

! Various reports ...
!Show voltages LN Nodes 
!Show Voltage LN Elements

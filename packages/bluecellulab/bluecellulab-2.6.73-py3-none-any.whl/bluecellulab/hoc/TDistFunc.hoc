/**
 * @file TDistFunc.hoc
 * @brief
 * @author rajnish
 * @date 2010-11-23
 * @remark Copyright 2005-2023 Blue Brain Project / EPFL
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * 
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

D__TDISTFUNCHOC = 1

begintemplate TDistFunc
    strdef stmp,distfunc,mech
    public distribute, ndistribute, distribute_section_variable

    proc distribute(){local x localobj sl
        sl = $o1
        mech = $s2
        distfunc = $s3
        sprint(distfunc,"%%s %s(%%f) = %s",mech, distfunc)
        //print distfunc
        forsec sl for(x,0) {
            sprint (stmp,distfunc,secname(),x,distance(x))
            //print stmp
            execute(stmp)
        }
    }

    proc distribute_section_variable(){ localobj sl
        sl = $o1
        mech = $s2
        distfunc = $s3
        sprint(distfunc,"%%s %s = %s",mech, distfunc)
        //print distfunc
        forsec sl {
            sprint (stmp,distfunc,secname(),distance(0.5))
            //print stmp
            execute(stmp)
        }
    }

    proc ndistribute(){local x,xroot,norm_factor localobj sl
            //print "Inside distribute .. "
            sl = $o1
            mech = $s2
            distfunc = $s3
            norm_factor = 1
            if(numarg()>3) norm_factor = getLongestBranch(sl)
            sprint(distfunc,"%%s %s(%%f) = %s",mech, distfunc)
            //print distfunc
            forsec sl for(x,0) {
                sprint (stmp,distfunc,secname(),x,distance(x)/norm_factor)
            //    print stmp
                execute(stmp)
            }
    }


    func getLongestBranch(){local maxL,d localobj distallist,sref
        forsec $o1 {
             distance()
             break
        }
        maxL = 0
        d = 0
        distallist = new SectionList()
        forsec $o1 {
            sref = new SectionRef()
            if (sref.nchild==0) distallist.append()
        }
        forsec distallist{
            d = distance(1)
            if(maxL<d) maxL = d
        }
        // for the soma case
        if (maxL == 0) {
            $o1 {
                maxL = L
            }
        }
        return maxL
    }
endtemplate TDistFunc

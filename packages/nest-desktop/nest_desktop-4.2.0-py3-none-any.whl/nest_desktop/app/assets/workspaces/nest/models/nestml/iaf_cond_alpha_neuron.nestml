model iaf_cond_alpha_neuron:

    state:
        V_m mV = -70 mV     # Membrane potential
        refr_t ms = 0 ms    # Refractory period timer
        is_refractory boolean = false

    parameters:
        C_m pF = 250 pF          # Membrane capacitance
        g_L nS = 16.6667 nS      # Leak conductance
        E_L mV = -70 mV          # Leak reversal potential (aka resting potential)
        refr_T ms = 2 ms         # Duration of refractory period
        V_th mV = -55 mV         # Spike threshold potential
        V_reset mV = -60 mV      # Reset potential

        E_exc mV = 0 mV          # Excitatory reversal potential
        E_inh mV = -85 mV        # Inhibitory reversal potential
        tau_syn_exc ms = 0.2 ms  # Synaptic time constant of excitatory synapse
        tau_syn_inh ms = 2 ms    # Synaptic time constant of inhibitory synapse

        # constant external input current
        I_e pA = 0 pA

    equations:
        kernel g_inh = (e/tau_syn_inh) * t * exp(-t/tau_syn_inh)
        kernel g_exc = (e/tau_syn_exc) * t * exp(-t/tau_syn_exc)

        inline I_syn_exc pA = convolve(g_exc, exc_spikes) * nS * ( V_m - E_exc )
        inline I_syn_inh pA = convolve(g_inh, inh_spikes) * nS * ( V_m - E_inh )
        inline I_leak pA = g_L * ( V_m - E_L )

        V_m' = ( -I_leak - I_syn_exc - I_syn_inh + I_e + I_stim ) / C_m

    input:
        exc_spikes <- excitatory spike
        inh_spikes <- inhibitory spike
        I_stim pA <- continuous

    output:
        spike

    update:
        if is_refractory:
            # neuron is absolute refractory, do not evolve V_m
            refr_t -= resolution()
        else:
            # neuron not refractory, so evolve all ODEs (including V_m)
            integrate_odes()

    onCondition(V_m >= V_th):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        is_refractory = true
        V_m = V_reset
        emit_spike()

    onCondition(is_refractory and refr_t <= resolution() / 2):
        # end of refractory period
        refr_t = 0 ms
        is_refractory = false

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX s223: <http://data.ashrae.org/standard223#>
PREFIX spkl: <http://speckle.systems/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX meta: <http://meta>
prefix x: <urn:anon:fake:>

CONSTRUCT {
?wire s223:cnx _:inconnect.
_:inconnect  a s223:InletConnectionPoint.
_:inconnect rdfs:label "InletConnectionPoint"^^xsd:string.
_:inconnect s223:hasMedium s223:Constituent-Electricity.
_:inconnect s223:cnx ?lum.
_:inconnect x:lum "lum". #to avoid duplicate insatnces causing validation error
}
WHERE {
?poe a s223:PowerOverEthernetSwitch. 
?poe s223:cnx ?conn.
?conn a s223:BidirectionalConnectionPoint.
?conn s223:cnx ?wire.
?wire a s223:Connection.
{
<<?poe spkl:category "Lighting Devices">> meta: <<?si spkl:name "electrical/poe switches">>. 
?poe spkl:parameters ?m1.
?m1 ?y1 ?circuit2.
?circuit2 spkl:applicationInternalName "RBS_ELEC_SWITCH_ID_PARAM".
?circuit2 spkl:value ?switchid.
}

{
?lum a s223:Luminaire.
<<?lum spkl:category  "Lighting Fixtures">> meta: <<?mi spkl:name "architecture/rooms and lighting fixtures">>.
?lum spkl:parameters ?m3.
?m3 ?y3 ?circuit3.
?circuit3 spkl:applicationInternalName "RBS_ELEC_SWITCH_ID_PARAM".
?circuit3 spkl:value ?switchid.
}
}
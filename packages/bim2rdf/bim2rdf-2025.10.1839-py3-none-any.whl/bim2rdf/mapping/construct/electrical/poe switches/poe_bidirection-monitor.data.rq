PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX s223: <http://data.ashrae.org/standard223#>
PREFIX spkl: <http://speckle.systems/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX meta: <http://meta>
prefix x: <urn:anon:fake:>

CONSTRUCT {
?luminairewire s223:cnx _:inletconn.
_:inletconn  a s223:InletConnectionPoint.
_:inletconn rdfs:label "InletConnectionPoint"^^xsd:string.
_:inletconn s223:hasMedium _:medium.
_:medium a s223:PowerAndSignal-PoE.
_:medium s223:composedOf _:elec.
_:elec a s223:Property.
_:elec s223:ofConstituent s223:Constituent-Electricity.
_:medium s223:composedOf _:sig.
_:sig a s223:Property.
_:sig s223:ofConstituent s223:Electricity-Signal.
_:inletconn s223:cnx ?mels.
_:inletconn x:m "m". #to avoid duplicate insatnces causing validation error
}
WHERE {
?poe a s223:PowerOverEthernetSwitch. 
?poe s223:cnx ?conn.
?conn a s223:BidirectionalConnectionPoint.
?conn s223:cnx ?luminairewire.
?luminairewire a s223:Connection.
{
<<?poe spkl:category "Lighting Devices">> meta: <<?si spkl:name "electrical/poe switches">>. 
?poe spkl:parameters ?m1.
?m1 ?y1 ?circuit2.
?circuit2 spkl:applicationInternalName "RBS_ELEC_SWITCH_ID_PARAM".
?circuit2 spkl:value ?switchid.
}
{
?mels a s223:Monitor.
<<?mels spkl:category  "Lighting Fixtures">> meta: <<?mi spkl:name "architecture/mels">>.
?mels spkl:parameters ?m3.
?m3 ?y3 ?circuit3.
?circuit3 spkl:applicationInternalName "RBS_ELEC_SWITCH_ID_PARAM".
?circuit3 spkl:value ?switchid.
}
}